apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+08:00"))
}
android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            libraryVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.name}" + "_v" + "${defaultConfig.versionName}_" + releaseTime().toString()+".aar"
                }
            }
        }
    }

    sourceSets {
        def dirs = ['p_scaleimageview', 'p_progressview']
        main {
            dirs.each { dir ->
                java.srcDir("src/${dir}/main/java")
                res.srcDir("src/${dir}/main/res")
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation dependenciesList.Appcompat
}

if (rootProject.file('gradle/publish-module.gradle').exists()) {
    apply from: rootProject.file('gradle/publish-module.gradle')
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = 'imageviewer'
            }
        }
    }
}